#### Owner Anyone

##### Descrição

Para gerenciar a propriedade de contratos, existe uma biblioteca muito utilizada, a _Owned_. Esta library se utiliza de _modifiers_ para proteção de funções privilegiadas dentro de um contrato. Porém a biblioteca possui um método _setOwner()_ que registra o proprietário do contrato, e este método, além de público, está totalmente desprotegido.

```
contract Owned {
  address public owner;
  function Owned() {
    owner = msg.sender;
  }
  function setOwner(address _owner) returns (bool success) {
    owner = _owner;
    return true;
  }
  modifier onlyOwner {
    require(msg.sender == owner);
    _;
  }
}
```

##### Exemplo

O contrato abaixo usa a biblioteca _Owned_ e implementa algumas funções protegidas. Porém como a função _setOwner()_ de _Owned_ não foi sobrescrita pelo contrato, qualquer um pode chamar esta função e mudar o _Owner_ deste contrato para em seguida executar _unlockToken()_ e fazer transferências para sua conta. Em seguida pode executar _lockBalances()_ e efetivamente fazer um **DoS** tornando o contrato permanentemente indisponível.

```
contract MeuContrato is Owned {
    bool public locked = true;
    bool public balancesUploaded = false;

    /**
     * Internal transfer, only can be called by this contract
     */
    function _transfer(address _from, address _to, uint _value) internal {
        require(!locked || msg.sender == owner);
        require(_to != 0x0);
        require(balanceOf[_from] >= _value);
        require(balanceOf[_to] + _value > balanceOf[_to]);
        uint previousBalances = balanceOf[_from] + balanceOf[_to];
        balanceOf[_from] -= _value;
        balanceOf[_to] += _value;
        Transfer(_from, _to, _value);
        require(balanceOf[_from] + balanceOf[_to] == previousBalances);
    }

    /**
     * Transfer tokens
     *
     * Send `_value` tokens to `_to` from your account
     *
     * @param _to The address of the recipient
     * @param _value the amount to send
     */
    function transfer(address _to, uint256 _value) public returns (bool success) {
        _transfer(msg.sender, _to, _value);
        return true;
    }

    function unlockToken() onlyOwner {
      locked = false;
    }

    function uploadBalances(address[] recipients, uint256[] balances) onlyOwner {
      require(!balancesUploaded);
      uint256 sum = 0;
      for (uint256 i = 0; i < recipients.length; i++) {
        balanceOf[recipients[i]] = safeAdd(balanceOf[recipients[i]], balances[i]);
        sum = safeAdd(sum, balances[i]);
      }
      balanceOf[owner] = safeSub(balanceOf[owner], sum);
    }

    function lockBalances() onlyOwner {
      balancesUploaded = true;
    }
}
```

##### Detecção

N/D

##### Mitigação

Ao utilizar a biblioteca _Owned_, sempre sobrescrever a função _setOwner()_ para uma versão mais segura ou aplicar um modificador para **privado** ou **ownerOnly** da própria biblioteca.

##### Fontes

* [PeckShield](https://peckshield.com)
* [Etherscan](https://etherscan.io)
